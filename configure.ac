dnl
dnl File:        configure.in
dnl Revision:    $Id: configure.ac,v 1.14 2010/06/14 12:50:57 j-nijhof Exp $
dnl Created:     2007/08/17
dnl Author:      Jeroen Nijhof <jeroen@jeroennijhof.nl>
dnl              R.K. Owen <rkowen@nersc.gov>
dnl
dnl Process this file with autoconf to produce a configure script
dnl You need autoconf 2.59 or better!
dnl
dnl ---------------------------------------------------------------------------

AC_PREREQ(2.59)
AC_COPYRIGHT([
See the included file: COPYING for copyright information.
])
AC_INIT(pam-script, 1.1.5, [jeroen@jeroennijhof.nl,rkowen@nersc.gov])
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE(1.9.6)
AC_CONFIG_SRCDIR([pam_script.c])
AC_CONFIG_HEADER([config.h])
AC_AIX

AM_DISABLE_STATIC

dnl --------------------------------------------------------------------
# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

dnl --------------------------------------------------------------------
# Checks for libraries.
AC_CHECK_LIB(pam,pam_start)
AC_CHECK_LIB(pam_misc,pam_misc_setenv)

dnl --------------------------------------------------------------------
dnl Autoheader "templates" (was acconfig.h)
AH_TOP([
/*****
 **** PAM-SCRIPT Header ************************************************
 **
 ** pam-script
 **	execute scripts or programs during the PAM stack phases:
 **		AUTH, ACCOUNT, SESSION, PASSWORD
 **
 ** Description: config.h.in (generated by autoheader from configure.ac)
 **
 ***********************************************************************
 ****/
])dnl

dnl --------------------------------------------------------------------
dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([string.h syslog.h unistd.h])
AC_CHECK_HEADERS([security/pam_appl.h], [],
	[AC_MSG_ERROR([*** Sorry, you must install the PAM development module ***])])
AC_CHECK_HEADERS([security/pam_modules.h], [], [],
	[#include <security/pam_appl.h>])

dnl --------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T

dnl --------------------------------------------------------------------
dnl Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_STAT
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(vsyslog)
if test $ac_cv_func_vsyslog = no; then
	# syslog is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet; do
	AC_CHECK_LIB($lib, vsyslog, [AC_DEFINE(HAVE_VSYSLOG)
		LIBS="$LIBS -l$lib"; break])
	done
fi
AC_CHECK_FUNCS([setenv putenv])


dnl --------------------------------------------------------------------
dnl Generate made files
AC_CONFIG_FILES([Makefile pam-script.7 pam-script.spec])
AC_OUTPUT
