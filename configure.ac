dnl
dnl File:        configure.in
dnl Revision:    $Id: configure.ac,v 1.3 2007/08/22 15:50:11 rkowen Exp $
dnl Created:     2007/08/17
dnl Author:      Jeroen Nijhof <jnijhof@nijhofnet.nl>
dnl              R.K. Owen <rkowen@nersc.gov>
dnl
dnl Process this file with autoconf to produce a configure script
dnl You need autoconf 2.59 or better!
dnl
dnl ---------------------------------------------------------------------------

AC_PREREQ(2.59)
AC_COPYRIGHT([
See the included file: COPYING for copyright information.
])
AC_INIT(pam-script, 1.1.0, [jnijhof@nijhofnet.nl,rkowen@nersc.gov])
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE(1.9.6)
AC_CONFIG_SRCDIR([pam_script.c])
AC_CONFIG_HEADER([config.h])
AC_AIX

AM_DISABLE_STATIC

dnl --------------------------------------------------------------------
# Checks for programs.
AC_PROG_CC
AC_PROG_LIBTOOL

dnl --------------------------------------------------------------------
# Checks for libraries.
AC_CHECK_LIB(pam,pam_start)
AC_CHECK_LIB(pam_misc,pam_misc_setenv)

dnl --------------------------------------------------------------------
dnl Autoheader "templates" (was acconfig.h)
AH_TOP([
/*****
 **** PAM-SCRIPT Header ************************************************
 **
 ** pam-script
 **	execute scripts or programs during the PAM stack phases:
 **		AUTH, ACCOUNT, SESSION, PASSWORD
 **
 ** Description: config.h.in (generated by autoheader from configure.ac)
 **
 ***********************************************************************
 ****/
])dnl

dnl --------------------------------------------------------------------
dnl Checks for header files.
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([security/pam_appl.h security/pam_modules.h],,
[AC_MSG_ERROR([*** Sorry, you must install the PAM development module ***])])

dnl --------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl --------------------------------------------------------------------
dnl Checks for library functions.
AC_FUNC_STAT
AC_CHECK_FUNCS([snprintf],,
[AC_MSG_ERROR([*** Sorry, no standard C99 snprintf() function found ***])])
AC_CHECK_FUNCS(vsyslog)
if test $ac_cv_func_vsyslog = no; then
	# syslog is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet; do
	AC_CHECK_LIB($lib, vsyslog, [AC_DEFINE(HAVE_VSYSLOG)
		LIBS="$LIBS -l$lib"; break])
	done
fi


dnl --------------------------------------------------------------------
dnl Generate made files
AC_CONFIG_FILES([Makefile])
AC_OUTPUT
